AC_PREREQ(2.52)
AC_INIT(liboobs, 2.22.1)
AC_CONFIG_SRCDIR(oobs/oobs-session.c)
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)

AM_CONFIG_HEADER(config.h)

AM_MAINTAINER_MODE

AM_PROG_LIBTOOL

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC
AC_PROG_INSTALL
AC_PROG_MAKE_SET

# libtool versioning
#
# See http://sources.redhat.com/autobook/autobook/autobook_91.html#SEC91 for details
#
LT_CURRENT=4
LT_REVISION=0
LT_AGE=0
AC_SUBST(LT_CURRENT)
AC_SUBST(LT_REVISION)
AC_SUBST(LT_AGE)

GLIB_REQUIRED=2.14.0
DBUS_REQUIRED=0.70
STB_REQUIRED=2.5.4
HAL_REQUIRED=0.5.9

dnl set gettext stuff

GETTEXT_PACKAGE=liboobs
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE",[The name of the gettext domain])

dnl Check for gtk-doc
GTK_DOC_CHECK([1.0])

dnl =====================================================
dnl required packages detection
dnl =====================================================
PKG_CHECK_MODULES(OOBS, [
		  dbus-glib-1 >= $DBUS_REQUIRED
		  glib-2.0    >= $GLIB_REQUIRED
		  gobject-2.0 >= $GLIB_REQUIRED
		  system-tools-backends-2.0 >= $STB_REQUIRED
		  ])

OOBS_CFLAGS="-DDBUS_API_SUBJECT_TO_CHANGE $OOBS_CFLAGS"

AC_SUBST(OOBS_LIBS)
AC_SUBST(OOBS_CFLAGS)

dnl =====================================================
dnl HAL detection
dnl =====================================================
PKG_CHECK_MODULES(HAL, hal >= $HAL_REQUIRED, have_hal=yes, have_hal=no)

if test "$have_hal" = "yes"; then
  AC_DEFINE(HAVE_HAL, [1], [whether HAL was found])
fi

AC_SUBST(HAL_LIBS)
AC_SUBST(HAL_CFLAGS)

AC_CHECK_LIB(crypt, crypt, , [AC_MSG_ERROR(crypt library is required.)])
AC_CHECK_HEADER(crypt.h, AC_DEFINE(HAVE_CRYPT_H, "", [whether it has crypt function]))

AC_MSG_CHECKING(whether rtnetlink exists)
AC_TRY_CPP([
#include <sys/types.h>
#include <sys/socket.h>
#include <linux/netlink.h>
#include <linux/rtnetlink.h>
], have_rtnetlink=yes, have_rtnetlink=no)

AC_MSG_RESULT($have_rtnetlink)
AM_CONDITIONAL(HAVE_RTNETLINK, test x$have_rtnetlink = xyes)

AC_OUTPUT([
Makefile
doc/Makefile
doc/reference/Makefile
doc/reference/version.xml
oobs/Makefile
liboobs-1.pc
])

echo "configuration:
        use netlink: ${have_rtnetlink}
        use HAL:     ${have_hal}
        doc build:   ${enable_gtk_doc}
"
